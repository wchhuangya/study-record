[TOC]

# 一、 批处理常用 DOS 命令

> 学习要点：
>
> 1. 显示语句：echo
> 2. 注释语句：rem
> 3. 目录切换：cd
> 4. 列文件名：dir
>
> 准备工作：先在 C 盘下建一个文件夹 bat，并建一个空的 bat 文件，名称就叫做：mybat.bat

## echo ：显示信息

1. echo 要输出的信息

   > 该方法打印出来的信息会显示 **命令的回显**

   ```shell
   echo hello,world
   pause
   
   C:\bat>echo hello,world
   hello,world
   C:\bat>pause
   请按任意键继续. . .
   ```
   
2. @echo 要输出的信息

   ```shell
   @echo hello,world
   pause
   
   hello,world
   请按任意键继续. . .
   ```

3. 打开回显或关闭回显功能：echo [on|off]

   > 该命令是显式的关闭/打开命令的回显

   ```shell
   @echo off
   echo hello,world
   pause
   
   hello,world
   请按任意键继续. . .
   ```

4. 输出空行，相当于一个回车：echo.

   > 注意：
   >
   > 1. 命令中的 "." 要紧跟在 echo 后面，中间不能有空格
   > 2. "." 可以用  : ; " / ] + \ 这些任一符号代替

   ```shell
   @echo off
   echo 我在写批处理教程哦！
   echo.
   echo 你看到上面有空一行么？
   pause
   
   我在写批处理教程哦！
   
   你看到上面有空一行么？
   请按任意键继续. . .
   ```

5. 答复命令中的提问：echo 回复|命令表达式

   > 解释：上面的例子是删除 c 盘的 abc 文件夹，系统会弹出删除确认的提示；下面的例子通过管道命令 | 把 “回复” 作为输入传导给后面的命令表达式，并作为命令表达式的输入

   ```shell
   @echo off
   rd /s c:\abc
   pause
   
   c:\abc, 是否确认(Y/N)?
   
   @echo off
   echo Y|rd /s c:\abc
   pause
   ```

6. 建立新文件或增加文件内容：echo 文件内容>文件名

   > 1. 下面的代码，新建一个名为：myfile.txt 的文件，并在里面写入内容：123；下面的代码建立了一个空的文件：myfle.txt
   > 2. 第二段代码中，echo 后面的 . 必须有，如果没有，文件内会显示：ECHO 处于关闭状态

   ```shell
   @echo off
   echo 123>myfile.txt
   pause
   
   @echo off
   echo. >myfile.txt 
   pause
   ```

## rem ：注释语句

rem 为注释命令，一般用来给程序加上注解，该命令后的内容不被执行，但能被回显

```shell
@echo off
echo 这是例1哦！
rem 上面的echo 语句用于显示提示。
pause
```

另外 :: 也可以起到 rem 的注释作用，而且更简洁有效

> 1. 任何以冒号 : 开头的字符行，在批处理中都被视作标号，而且直接忽略其后的所有内容
>    1. 有效标号：冒号后面紧跟一个字母数字开头的字符串，goto 语句可以识别
>    2. 无效标号：冒号后面紧跟一个以非字母数字的一个特殊符号，goto 语句无法识别的标号，可以直到注释作用
> 2. 与 rem 不同的是，:: 后面的字符串在执行时不会回显，无论是否用 echo on 打开命令行回显状态，因为命令解释器不认识它是一个有效的命令行，就此点来看，rem 在某些场合下将比 :: 更为适用；另外，rem 可以用于 config.sys 文件中

```shell
@echo off
echo 这是例2哦！
::上面的echo 语句用于显示提示。
pause
```

## cd ：目录切换

1. 同一个分区的切换：`cd [盘符][路径]`

   ```shell
   切换到C:\WINDOWS\system32目录 
   C:\Documents and Settings\mzybar>               
   cd C:\WINDOWS\system32
   C:\WINDOWS\system32>
   ```

2. 返回上一级：`cd..`

   ```shell
   C:\WINDOWS\system32>
   cd..
   C:\WINDOWS>
   ```

3. 返回根目录：`cd\`

   > 其实在DOS命令中.、..和\都是表示相对路径的，.是当前目录，..是上一层目录，而\则表示根目录

   ```shell
   C:\WINDOWS\system32>
   cd\
   C:\ >
   ```

4. 不同分区的切换：`cd /d [盘符][路径]`

   ```shell
   切换到D:\123\abc
   C:\Documents and Settings\mzybar>
   cd /d d:\123\abc
   D:\123\abc>
   ```

5. 要切换到某个盘符的根目录：`盘符号:`

   ```shell
   C:\Documents and Settings\mzybar>
   D:
   D:\>
   ```

6. 显示当前的完整路径：`%cd%`

   ```shell
   @echo off
   echo 当前路径是 %cd%
   pause
   ```

## dir ：列文件名

1. 显示目录中的文件和子目录列表：`dir [drive:][path][filename][/A[[:]attributes]][/B][/C][/D][/L][/N][/O[[:]sortorder]][/P][/Q][/S][/T[[:]timefield]][/W][/X][/4]`

2. 属性及说明

   | 属性       | 说明                                                         |
   | ---------- | ------------------------------------------------------------ |
   | /A         | 显示具有指定属性的文件                                       |
   | attributes |                                                              |
   | D          | 目录                                                         |
   | R          | 只读文件                                                     |
   | H          | 隐藏文件                                                     |
   | A          | 准备存档的文件                                               |
   | S          | 系统文件                                                     |
   | -          | 表示“否”的前缀                                               |
   | /B         | 使用空格符（没有标题信息或摘要）                             |
   | /C         | 在文件大小写中显示千位数分隔符，这是默认值，用 /C 来信用分隔符显示 |
   | /D         | 跟宽式相同，但文件是按栏分类列出的                           |
   | /L         | 用小写                                                       |
   | /N         | 新的长列表格式，其中文件名在最右边                           |
   | /O         | 用分类顺序列出文件                                           |
   | shotorder  |                                                              |
   | N          | 按名称（字母顺序）                                           |
   | S          | 按大小（从小到大）                                           |
   | E          | 按扩展名（字母顺序）                                         |
   | D          | 按日期/时间（从先到后）                                      |
   | G          | 组目录优先                                                   |
   | -          | 颠倒顺序的前缀                                               |
   | /P         | 在每个信息屏幕后暂停                                         |
   | /Q         | 显示文件所有者                                               |
   | /S         | 显示指定目录和所有子目录中的文件                             |
   | /T         | 控制显示或用来分类的时间字符域                               |
   | timefield  |                                                              |
   | C          | 创建时间                                                     |
   | A          | 上次访问时间                                                 |
   | W          | 上次写入的时间                                               |
   | /W         | 用宽列表格式                                                 |
   | /X         | 显示为非 8dot3 文件名产生的短名称。格式是 /N 的格式，短名称插在长名称前面。如果没有短名称，在其位置则显示空白 |
   | /4         | 用 4 位数字显示年                                            |

3. `[drive:][path][filename]` ：指定要列出的驱动器、目录和/或文件

   > 1. 添加 `/p` 参数可以在每个信息屏幕后暂停
   >
   > 2. /B 使用空格式(没有标题信息或摘要)。

   ```shell
   dir c:\windows
   ```

4. 列出指定属性的文件/夹：`dir /a[:] [D|R|H|A|S|-DRHAS]`

   ```shell
   列出 C 盘下所有的文件
   dir /a c:\
   列出隐藏的文件（包含文件和文件夹）
   dir /ah c:\
   列出 C 盘下所有的文件夹
   dir /ad c:\
   列出 C 盘下所有的文件夹，但不包含隐含的文件夹
   ```

5. 按分类列出文件：`dir /o[:] [N|S|E|D|G|-NSEDG]`

   ```shell
   按文件名的字母顺序排序
   dir /on c:\
   优先列出文件夹，并且文件夹和文件都按字母顺序排序
   dir /ogn c:\
   ```

# 二、常用网络命令和符号篇

## 网络命令

> 学习要点：
>
> 1. 测试 ip 连接：ping
> 2. 查看 ip 配置：ipconfig
> 3. 解析地址：arp
> 4. 开启命令：start
> 5. 关闭或重启计算机：shutdown
> 6. 关闭指定进程：taskkill

### ping ：测试 ip 连接

用来检查网络是否通畅或者网络连接速度的命令。

基本参数：

1. `-n count` ：发送 count 指定的 echo 数据包数
2. `>nul` ：屏蔽输出（PS：即在屏幕上不输出）
3. `-w timeout` ：指定超时间隔，单位为毫秒

> 以上这三个指令配合起来，可用在批处理用于延时

```shell
C:\bat>ping -n 3 www.baidu.com

正在 Ping www.a.shifen.com [220.181.38.150] 具有 32 字节的数据:
来自 220.181.38.150 的回复: 字节=32 时间=30ms TTL=128
来自 220.181.38.150 的回复: 字节=32 时间=30ms TTL=128
来自 220.181.38.150 的回复: 字节=32 时间=86ms TTL=128

220.181.38.150 的 Ping 统计信息:
    数据包: 已发送 = 3，已接收 = 3，丢失 = 0 (0% 丢失)，
往返行程的估计时间(以毫秒为单位):
    最短 = 30ms，最长 = 86ms，平均 = 48ms
```

### ipconfig ：查看 ip 配置

当使用 IPConfig 不带任何参数选项时，那么它为每个已经配置了的接口显示 IP 地址、子网掩码和缺省网关值。

参数：

`/all` ：显示所有适配器的完整 TCP/IP 配置信息。如果有多个网卡时，此命令都会将它们的信息一一列出来

```shell
C:\bat>ipconfig /all

Windows IP 配置

   主机名  . . . . . . . . . . . . . : DDF7
   主 DNS 后缀 . . . . . . . . . . . :
   节点类型  . . . . . . . . . . . . : 混合
   IP 路由已启用 . . . . . . . . . . : 否
   WINS 代理已启用 . . . . . . . . . : 否
   DNS 后缀搜索列表  . . . . . . . . : localdomain

以太网适配器 本地连接:

   连接特定的 DNS 后缀 . . . . . . . : localdomain
   描述. . . . . . . . . . . . . . . : Intel(R) PRO/1000 MT Network Connection
   物理地址. . . . . . . . . . . . . : 00-1C-42-C8-85-23
   DHCP 已启用 . . . . . . . . . . . : 是
   自动配置已启用. . . . . . . . . . : 是
   IPv6 地址 . . . . . . . . . . . . : fdb2:2c26:f4e4:0:b58f:1708:535d:c393(首选)
   临时 IPv6 地址. . . . . . . . . . : fdb2:2c26:f4e4:0:bc85:6a53:adac:eb7b(首选)
   本地链接 IPv6 地址. . . . . . . . : fe80::b58f:1708:535d:c393%11(首选)
   IPv4 地址 . . . . . . . . . . . . : 10.211.55.3(首选)
   子网掩码  . . . . . . . . . . . . : 255.255.255.0
   获得租约的时间  . . . . . . . . . : 2019年9月8日 19:07:43
   租约过期的时间  . . . . . . . . . : 2019年9月8日 21:07:43
   默认网关. . . . . . . . . . . . . : fe80::21c:42ff:fe00:18%11
                                       10.211.55.1
   DHCP 服务器 . . . . . . . . . . . : 10.211.55.1
   DHCPv6 IAID . . . . . . . . . . . : 234888258
   DHCPv6 客户端 DUID  . . . . . . . : 00-01-00-01-23-73-02-1A-00-1C-42-C8-85-
   DNS 服务器  . . . . . . . . . . . : fe80::21c:42ff:fe00:18%11
                                       10.211.55.1
   TCPIP 上的 NetBIOS  . . . . . . . : 已启用

隧道适配器 isatap.localdomain:

   媒体状态  . . . . . . . . . . . . : 媒体已断开
   连接特定的 DNS 后缀 . . . . . . . : localdomain
   描述. . . . . . . . . . . . . . . : Microsoft ISATAP Adapter
   物理地址. . . . . . . . . . . . . : 00-00-00-00-00-00-00-E0
   DHCP 已启用 . . . . . . . . . . . : 否
   自动配置已启用. . . . . . . . . . : 是

隧道适配器 Teredo Tunneling Pseudo-Interface:

   媒体状态  . . . . . . . . . . . . : 媒体已断开
   连接特定的 DNS 后缀 . . . . . . . :
   描述. . . . . . . . . . . . . . . : Teredo Tunneling Pseudo-Interface
   物理地址. . . . . . . . . . . . . : 00-00-00-00-00-00-00-E0
   DHCP 已启用 . . . . . . . . . . . : 否
   自动配置已启用. . . . . . . . . . : 是
```

### arp ：解析地址

显示和修改“地址解析协议 (ARP)”缓存中的项目。ARP 缓存中包含一个或多个表，它们用于存储 IP 地址及其经过
解析的以太网或令牌环物理地址。计算机上安装的每一个以太网或令牌环网络适配器都有自己单独的表。

参数：

1. `-a|-g [ip]` ：用于查看高速缓存中的所有项目。-a和-g参数的结果是一样的，多年来-g一直是UNIX平台上
   用来显示ARP高速缓存中所有项目的选项，而Windows用的是arp -a（-a可被视为all，即全部的意思），但它也可以接受比较传统的 -g 选项

   1. 如果我们有多个网卡，那么使用arp –a 加上对应网卡的IP地址，就可以只显示与该网卡相关的ARP缓存项目
   2. 缺省IP情况下，将显示所有当前 ARP 缓存表

   ```shell
   C:\bat>arp -a
   
   接口: 10.211.55.3 --- 0xb
     Internet 地址         物理地址              类型
     10.211.55.1           00-1c-42-00-00-18     动态
     10.211.55.2           00-1c-42-00-00-08     动态
     10.211.55.255         ff-ff-ff-ff-ff-ff     静态
     224.0.0.22            01-00-5e-00-00-16     静态
     224.0.0.251           01-00-5e-00-00-fb     静态
     224.0.0.252           01-00-5e-00-00-fc     静态
     239.255.255.250       01-00-5e-7f-ff-fa     静态
     255.255.255.255       ff-ff-ff-ff-ff-ff     静态
     
   分析一下上面的数据：
   Internet Address：代表 IP 地址。
   Physical Address：代表网卡的物理地址，即网卡的MAC地址。
   Type：代表ARP表的类型，“dynamic”为动态的；“static”为静态的。
   ```

2. `arp -s IP 物理地址` ：我们可以向ARP高速缓存中人工输入一个静态项目。该项目在计算机引导过程中将保持有效状态，或者在出现错误时，人工配置的物理地址将自动更新该项目。通过此命令可以实现ARP绑定

   ```shell
   arp -s 192.168.1.1 00-1a-92-90-19-fe
   对192.168.1.1添加静态ARP实现ARP绑定。
   ```

3. `arp -d IP` ：使用本命令能够人工删除一个静态项目。可以输入arp -d命令，缺省IP情况下将删除当前计算机的arp表。在对付ARP欺骗中，我们一般先删除当前计算机的arp表，然后再实现ARP绑定

   ```shell
   @echo off
   arp –d
   arp –s 192.168.0.1 00-1a-92-90-19-fe
   arp –s 192.168.0.2 00-1a-92-90-19-eb
   arp –s 192.168.0.3 00-1a-92-49-1c-a9
   arp –s 192.168.0.4 00-1a-92-90-19-32
   arp –s 192.168.0.5 00-1a-92-38-07-09
   pause
   ```

### start ：开启命令

`start “title” filename` ：其中title可以省略，filename如带有空格或是特殊符号的，用""括起来

参数：

1. `/min|max` ：使开启的窗口最小化|最大化。

```shell
start http://www.baidu.com
这里是打开百度，title省略了。

start "" "%windir%\system32\NOTEPAD.EXE" "%userprofile%\桌面\abc.txt"
用NOTEPAD（记事本）打开 桌面上的abc.txt。

start "" "%userprofile%\桌面\abc.doc"
这里start后面要加""才能启动文件abc.doc，否则不能启动。
如果start 后没有 " " 则表示把title省略了，此时文件名若有""的话start就会把它看作是标题，从而变成了省略文件名，就默认开启cmd了。所以在start后加上""就能防止这种意外的情况。

start /min "" "%windir%\system32\NOTEPAD.EXE" "%userprofile%\桌面\abc.txt"
用记事本最小化打开桌面的abc.txt。
```

### shutdown ：关闭或重启计算机

`shutdown [-i | -l | -s | -r | -a] [-f] [-m \\computername] [-t xx] [-c "comment"] [-d up:xx:yy]`

| 命令                | 说明                                  |
| ------------------- | ------------------------------------- |
| 没有参数            | 显示帮助信息，与加 ? 参数作用相同     |
| -i                  | 显示 GUI 界面，必须是第一个选项       |
| -l                  | 注销（不能与选项 -m 一起使用）        |
| -s                  | 关闭此计算机                          |
| -r                  | 关闭并重启动此计算机                  |
| -a                  | 放弃系统关机                          |
| -m \\\\computername | 远程计算机关机/重启动/放弃            |
| -t xx               | 设置关闭的超时为 xx 秒                |
| -c "comment"        | 关闭注释（最大127个字符）             |
| -f                  | 强制运行的应用程序关闭而没有警告      |
| -d \[u]\[p]:xx:yy   | 关闭原因代码                          |
| u                   | 用户代码                              |
| p                   | 一个计划的关闭代码                    |
| xx                  | 一个主要原因代码（小于256的正整数）   |
| yy                  | 一个将要原因代码（小于65535的正整数） |

```shell
shutdown -l
该命令只能注销本机用户，对远程计算机不适用。

at 12:00 shutdown -s
通过-s参数，在12：00执行关闭计算机。

shutdown -r -t 60
60秒后重启计算机。这里是设置重启倒计时为60秒。如果没有设置时间则默认为30秒。

shutdown -f -s -t 300
通过-f参数强制计算机300秒后关闭。

shutdown -a
当我们在遇到冲击波或震荡波病毒时，或是执行了上面的关机命令时，我们可以运用shutdown -a 终止当前的关机进程，这样就可以避免关机而造成的损失了。
```

### taskkill ：关闭指定进程

可以根据进程 ID 或图像名来结束一个或多个任务或进程

参数：

1. `/im ImageName` ：指定将终止进程的图像名称
2. `/f process` ：指定将强制终止的进程。对于远程进程可忽略此参数，所有远程进程都将被强制终止
3. `/pid process_id` ：指定要终止的进程的PID

```shell
taskkill /im qq.exe

taskkill /f /im spoolsv.exe

taskkill /pid 1230 /pid 1241 /pid 1253
这里是终止PID 分别为 1230 1241 1253的进程。
```

## 数个常用的特殊符号

> 学习要点：
>
> 1. \>、\>> 重定向符
> 2. | 命令管道符
> 3. &、&&、|| 组合命令
> 4. ^ 转义字符
> 5. % 变量引导符
> 6. "" 界定符
>
> 组合命令和重定向命令、管道命令一起使用必须注意优先级：
>
> 管道命令的优先级高于重定向命令，重定向命令的优先级高于组合命令

### \>、\>> 重定向符

\> ：意思就是传递并覆盖，它所起的作用是将运行的结果传递到后面的范围（后面可以是文件，也可以是默认的控制台，即命令提示符）

> 如果下面的例子中1.txt原来有内容的话，在使用了> 重定向符命令后，将会被新的内容覆盖掉，这个一定要注意

```shell
echo hello120 >1.txt
新建立文件 1.txt，内容为”hello120” （注意：行尾有一空格,文件后有一空行）

(echo hello120)>1.txt   新建立文件 1.txt，内容为”hello120” （注意：行尾无空格,但文件后有一空行）
>1.txt echo hello120    新建立文件 1.txt，内容为”hello120” （注意：行尾无空格,但文件后有一空行）

>1.txt <nul set /p=hello120  
>1.txt set /p=hello120<nul
新建立文件 1.txt，内容为”hello120” （注意：行尾无空格,且文件后也无空行）
```

\>> ：重定向符 将命令的输出结果追加到其后面的设备中去。这个符号的作用和>有点类似，但他们是有区别的。 \>>是传递并在文件的末尾追加，而>是覆盖。用法同 \> 一样的

```shell
echo hello120 >>1.txt
如果1.txt不存在，则新建立文件 1.txt，内容为”hello120 ”；如果1.txt存在，则把内容为”hello120 ”追加
到其文末尾。

echo hello> 1.txt
echo world>>1.txt
这时候 1.txt内容如下: 
hello
world
```

### | 命令管道符

表示把在它之前的命令或语句的执行结果作为在它之后的命令或语句的处理对象，简而言之，就是把它之前的输出作为它之后的输入。

```shell
tasklist | find /i "qq.exe" && taskkill /f /im qq.exe
将tasklist（进程列表）用传递符"|" 传递给find 命令，执行查找QQ的进程。

echo Y|rd /s c:\abc
通过管道命令|将echo y 的结果传给rd /s c:\abc 命令，从而达到自动输入y 的目的
```

### &、&&、|| 组合命令

组合命令顾名思义，就是可以把多个命令组合起来当一个命令来执行。

1. & ：允许同时执行多条命令，当第一个命令执行失败了，也不影响后边的命令执行。这里 & 两边的命令是顺序执行的，从前往后执行。格式：第一条命令 & 第二条命令 [& 第三条命令...]
2. && ：允许同时执行多条命令，当碰到执行出错的命令后将不再执行后面的命令，如果一直没有出错则一直执行完所有命令。格式：第一条命令 && 第二条命令 [&& 第三条命令...]
3. || ：允许同时执行多条命令，当一条命令执行失败后才执行第二条命令，当碰到执行正确的命令后将不执行后面的命令，如果没有出现正确的命令则一直执行完所有命令。格式：第一条命令 || 第二条命令 [|| 第三条命令...]

```shell
dir z:\ & dir y:\ & dir c:\
以上命令会连续显示z,y,c 盘的内容，不理会该盘是否存在。

dir z:\ && dir y:\ && dir c:\
这个命令和上面的类似，但区别是，前一个命令执行失败时，后边的命令就不会再执行了。

tasklist | find /i "qq.exe" && taskkill /f /im qq.exe || echo 你开了QQ?我不信
通过&&和||两个符号可以认为把上例分成了三个命令：
第一个命令:：tasklist | find /i "qq.exe"
第二个命令：taskkill /f /im qq.exe
第三个命令：echo 你开了QQ?我不信
这里我们分析一下，
如果找不到QQ进程，即第一个命令执行失败，则 && 不会执行第二个命令：taskkill命令，由于taskkill 命令没有执行，视作第二个命令执行失败，则 || 将会执行第三个命令：echo 命令。
如果找到QQ进程，即第一个命令执行成功，则 && 将会执行第二个命令：taskkill命令，由于第二个命令执行成功，|| 将不会执行第三个命令：echo 命令。
```

### ^ 转义字符

1. 在cmd环境中，有些字符具备特殊功能，如 >、>>表示重定向，| 表示管道，&、&&、|| 表示语句连接……它
   们都有特定的功能，如果需要把它们作为字符输出的话，echo >、echo | ……之类的写法就会出错——cmd解释器会把它们作为具有特殊功能的字符对待，而不会作为普通字符处理，这个时候，就需要对这些特殊字符做转义处理：在每个特殊字符前加上转义字符^。   因此，要输出这些特殊字符，就需要用 echo ^>、echo ^|、echo ^|^|、echo ^^……之类的格式来处理；   
2. 转义字符放在行尾，可以当作续行符号

```shell
@echo off 
echo 这是^
一个^
句子
Pause
```

### % 变量引导符

1. 当百分号成对出现，并且其间包含非特殊字符时，一般做变量引用处理，比如：%var%、%str%
2. 百分号作为变量引用还有一种特殊形式，那就是对形式参数的引用，此时，单个百分号后面紧跟0～9这10
   个数字，如%0、%1、%2 。。。
   1. %0 ：表示批处理文件本身，包括完整的路径和扩展名
   2. %1至%9 ：表示第一个参数至第九个参数
3. 出现在 set /a 语句中时，表示两数相除取余数，也就是所谓的模运算，它在命令行窗口和批处理文件中的写法
   略有差异：在命令行窗口中，只需要单个的%，在批处理文件中，需要连续两个百分号，写成%%。例如：在命令行窗口中，运行 set /a num=4%2 ，则结果将显示0，因为4除以2的余数为0；如果保存为批处理文件，则此语句将略有改变
4. 转义符号：如果要显示%本身时，需要在前面用%来转义

```shell
@echo off
set str=abc
echo 变量 str 的值是： %str%
pause

变量 str 的值是： abc
按任意键继续...

@echo off
if defined str goto next
set str=
set /p str=请把文件拉到本窗口后回车：
call "%~0" %str%
pause
:next
cls
echo 本批处理文件完整路径为："%~0"
echo 拖到本窗口的文件完整路径为："%~1"
goto :eof

@echo off
set /a num=4%%2
echo 4除以2的余数为 %num%
pause

@echo off
echo 一个百分号：%%
echo 两个百分号：%%%%
echo 三个百分号：%%%%%%
pause
```

### "" 界定符

1. 在表示带有空格或特殊符号的路径时常要用""来将路径括起来
2. 表示其中的内容是一个完整的字符串

```shell
cd /d cd "d:\program files\^%*abc"
因为路径中带有空格和特殊符号，所以要用""将路径括起来。

@echo off
set "var=abc 123"
echo %var%
pause
这里表示变量var的值为字符串“abc 123”。
```

# 三、批处理高级语句篇

## if 条件语句

> 学习要点：
>
> 1. 判断两个字符串是否相等
> 2. 判断两个数值是否相等
> 3. 判断驱动器、文件或文件夹是否存在
> 4. 判断变量是否已经定义
> 5. 判断上个命令的返回值

### 判断两个字符串是否相等

`if "字符串1"=="字符串2" command 语句` 

参数：

1. `/i` ：在进行比较时不区分大小写

```shell
@echo off
set /p var1=请输入第一个比较的字符：
set /p var2=请输入第二个比软的字符：
if "%var1%"=="%var2%" (echo 输入的两个字符相同) else echo 输入的两个字符不相同
pause
执行后会要求你输入两个字符串，然后批处理判断它俩是否相同。在判断字符串是否相等的时候，if是会区分大小写
的

@echo off
if "a"=="A" (echo 输入的两个字符相同) else echo 输入的两个字符不相同
pause
执行后会显示：输入的两个字符不相同

@echo off
if /i "a"=="A" (echo 输入的两个字符相同) else echo 输入的两个字符不相同
pause
这次执行后会显示：输入的两个字符相同
```

### 判断两个数值是否相等

`if 数值1 equ 数值2 command 语句`

| 中文含义   | 关系符 | 英文解释              |
| ---------- | ------ | --------------------- |
| 等于       | equ    | equal                 |
| 大于       | gtr    | greater than          |
| 大于或等于 | geq    | greater than or equal |
| 小于       | lss    | less than             |
| 小于或等于 | leq    | less than or equal    |
| 不等于     | neq    | no equal              |

> 批处理中的大于号，小于号，等于号等都不能用：“>”  “<”  “=” 这些符号，而要用像"gtr"这类的

```shell
@echo off 
set /p var=请输入一个数字: 
if %var% geq 10 (echo 此数大于或等于10) else echo 此数小于10
pause
```

### 判断驱动器、文件、文件夹是否存在

`if exist filename command 语句`

```shell
@echo off 
if exist "e:" (echo e盘存在) else echo e盘不存在 
pause>nul

@echo off
dir /a-d d:\123.bat >nul 2>nul
if %errorlevel%==0 (echo 123.bat文件存在！) else echo 123.bat文件不存在！ 
Pause
先用dir的/a-d参数去除123.bat的目录属性，指定说明要搜索的123.bat是文件而不是文件夹，并把结果（包括正确和错误）屏蔽(>nul 2>nul)，如果dir找到了文件123.bat，那么其errorlevel值(dir命令的退出编码)会被设为0，否则为1则是没有此文件。当然也可以用||和&&来判断。

@echo off
if exist test\ (echo test 是文件夹) else echo test 是文件
pause
```

#### 文件夹存在的两种判断

```shell
@echo off
if exist test\. (echo test 是文件夹) else echo test 是文件
pause

@echo off
if exist test\nul (echo test 是文件夹) else echo test 是文件
pause
```

### 判断变量是否已经定义

> 当变量不存在或是值为空时，变量则为未定义。
> 当使用 `if defined 变量 command 语句` 判断变量是否被定义时，请注意 变量为不使用引导符号%的变量名，不能用写为%变量%，否则出错。

```shell
@echo off
if defined a (echo 变量 a 已定义) else (echo 变量 a 没有被定义)
pause
执行后显示：变量 a 没有被定义

@echo off
set a=
if defined a (echo 变量 a 已定义) else (echo 变量 a 没有被定义)
pause
执行后显示：变量 a 没有被定义
注意：set a=这后面是没有空格的。

@echo off
set a=10
if defined a (echo 变量 a 已定义) else (echo 变量 a 没有被定义)
pause
执行后显示：变量 a 已定义

@echo off
set var1=5
if defined var1 (echo 变量var1已定义) else (echo 变量var1没有被定义)
set /p var2=请输入一个数字:
if defined %var2% (echo 变量var2已定义) else (echo 变量var2没有被定义)
pause
变量var1已定义
请输入一个数字:12
变量var2没有被定义
请按任意键继续. . .

@echo off
set var1=5
if defined var1 (echo 变量var1已定义) else (echo 变量var1没有被定义)
set /p var2=请输入一个数字:
if defined var2 (echo 变量var2已定义) else (echo 变量var2没有被定义)
pause
变量var1已定义
请输入一个数字:12
变量var2已定义
请按任意键继续. . .
```

### 判断上个命令的返回值

`if errorlevel 数值 command 语句`

> if errorlevel  语句的特点：
> 当使用  if errorlevel 0 cmmand 句式时，它的含义是：如果返回的错误码值大于或等于0 的时候，将执行cmmand操作；
> 当使用  if %errorlevel%==0 cmmand 句式时，它含义是：如果返回的错误码值等于0 的时候，将执行cmmand操作。

```shell
@echo off 
net user 
if %errorlevel% == 0 (echo net user 命令执行成功) else (echo net user 命令执行失败) 
Pause

@echo off 
set /p input=请输入任意一条命令： 
if errorlevel 0 (echo %input% 命令执行成功) else (echo %input% 命令执行失败)
pause
用这种语法,不管你前面的命令，是否执行成功,它都会认为命令成功了
```

### 补充：if not 语句

对于if 语句前面的 5 种基本语法，都可以加上not参数

## set 命令详解

> 学习要点：
>
> 1. 用 set 命令设置自定义变量
> 2. 用 set 命令进行字符串处理
> 3. 用 set 命令进行简单计算

### 用 set 命令设置自定义变量

`set [variable=[string]]` ：variable 指定环境变量名；string 指定要指派给变量 variable 的一系列字符串

> 1. 清除变量的值：variable=  注意：=后面无任何符号
> 2. set variable=""，这句不是把变量值赋值为空，而是让变量的值等于两个引号
> 3. /p ：set还可以提供一个交互界面,让用户自己输入变量的值，然后我们在来根据这个值来做相应操作，这种语法只需要加一个"/P"参数就可以了

```shell
@echo off 
set var=abcd 
echo %var% 
pause

set var=abcd 是批处理设置变量的方法。Var是变量名，而abcd则是变量名var的值。echo %var% ，在批处理中我们要引用这个变量就把var 变量名用两个%(百分号)扩起来,如%var%

@echo off 
set var=
set var1=""
if defined var (echo var的值为%var%) else echo var没有赋值
if defined var1 (echo var1的值为%var1%) else echo var1没有赋值
pause

@echo off 
set /p var=请输入变量的值: 
echo  您输入了%var% 
pause
```

### 用 set 命令进行字符串处理

`%PATH:str1=str2%` ：字符串替换，将字符串变量%PATH%中的str1 替换为str2

```shell
@echo off
set a= bbs. bathome. cn
echo  替换前的值: "%a%"
set var=%a: =%
echo  替换后的值: "%var%"
pause

替换前的值: " bbs. bathome. cn" 
替换后的值: "bbs.bathome.cn"

@echo off
set a= bbs.bathome.cn
echo  替换前的值: "%a%"
set var=%a:.=小情%
echo  替换后的值: "%var%"
pause

替换前的值: " bbs.bathome.cn"
替换后的值: " bbs小情bathome小情cn"
```

### 字符串截取

`%a:~[m[,n]]%` ：方括号表示可选，%为变量标识符，a为变量名，不可少，冒号用于分隔变量名和说明部分，符号～可以简单理解为“偏移”即可，m 为偏移量（缺省为0），n 为截取长度（缺省为全部）

```shell
@echo off
set a=bbs.bathome.cn
set var=%a:~1,3%
echo %var%
pause

执行显示为：bs.
set var=%a:~1,3% ，这里%a:~1,3%就是说变量 a  偏移量为1,要截取它的3位字符长度。合起来就是把变量a 的值从第2 位（偏移量 1）开始,截取3 个字符赋予变量var

@echo off
set a=bbs.bathome.cn
set var=%a:~1,-3%
echo %var%
pause

执行显示：bs.bathome
这里就是把变量a 第2位起到倒数第3位前的值赋予var

@echo off
set a=bbs.bathome.cn
set var=%a:~-3%
echo %var%
pause

执行显示：.cn
这里就是就是把变量a 倒数 3 位的值赋予变量var

@echo off
set a=bbs.bathome.cn
set var=%a:~-3,2%
echo %var%
pause

执行显示：.c
这里就是就是把变量a 倒数 3 位的值的前2位赋予变量var

@echo off 
set a=bbs.bathome.cn
set var=%a:~3%
echo %var%
pause

执行显示：.bathome.cn
这里就是把变量a 从第3 位开始后面全部的值给变量var
```

### 用 set 命令进行简单计算

`set /a expression` ：/a 命令行开关指定等号右边的字符串为被评估的数字表达式。该表达式评估器很简单并以递减的优先权顺序支持下列操作:

> 1. DOS 计算只能进行整数运算，精确到整数，它是不支持浮点计算的
> 2. 求余数运算符%在批处理程序中必须写成%%

| 符号                      | 说明           |
| ------------------------- | -------------- |
| ()                        | 分组           |
| !   ~   -                 | 一元运算符     |
| *   /   %                 | 算数运算符     |
| -                         | 算数运算符     |
| <<    >>                  | 二进制逻辑位移 |
| &                         | 二进制按位与   |
| ^                         | 二进制按位异   |
| \|                        | 二进制按位或   |
| =   *=   /=   %=   =   -= | 算数赋值       |
| &=  ^=  \|=  <<=  >>=     | 二进制运算赋值 |
| ,                         | 表达式分隔符   |

```shell
@echo off
set /p input=请输入计算表达式：
set /a var=%input%
echo  计算结果：%input%=%var%
pause

请输入计算表达式：1+9+20+30-10 
计算结果：1 9 20 30-10=50 
请按任意键继续. . .

@echo off
set /a a=1+1,b=2+1,c=3+1
echo  计算结果：%a% %b% %c%
pause

计算结果：2 3 4
```

## for 循环语句

### 基本语法

在cmd窗口中：`for %I in (command1) do command2 %I`
在批处理文件中：`for %%I in (command1) do command2 %%I`

要素：

1. for、in、do 三个是 for 语句的关键字，缺一不可
2. %%I 是 for 语句中对形式变量的引用，就算它在 do 后面的语句中没有参与语句的执行，也是必须出现
3. in 之后、do 之前的括号不能省略
4. %%I 中的 I，即形式变量，可以换成 26 个字母中的任意一个，字母区分大小写；也可以换为其它字符，但是请不要换为 0 ~ 9 的数字
5. in 和 do 之间的 command1表示的字符串或变量可以是一个，也可以是多个，每一个字符串或变量，我们称之为一个元素，每个元素之间，用空格键、跳格键、逗号、分号或等号分隔
6. for语句依次提取command1中的每一个元素，把它的值赋予形式变量I,带到do后的command2中参与命令的执行；并且每次只提取一个元素，然后执行一次do后的命令语句，而无论这个元素是否被带到command2中参与了command2的运行；当执行完一次do后的语句之后，再提取command1中的下一个元素，再执行一次command2，如此循环，直到command1中的所有元素都已经被提取完毕，该for语句才宣告执行结束

### for /f ：文本解析

作用：读取文件内容；提取某几行字符；截取某个字符片段；对提取到的内容再切分、打乱、杂糅……

**重点** 

1. for /f 语句是以行为单位处理文本文件的

2. delims= ：指定分隔符的方法；可以指定多个分隔符；如果不指定，则默认以空格键或跳格键作为分隔符
3. for /f 语句默认只提取第一节的字符串

**意义**

1. 指定原文中的标点符号作为分隔符。使得文本被划分为许多小部分，方便使用批处理命令读取和编辑。
2. 读取第一个分隔符之前的内容。忽略第一个分隔符和分隔符后面的内容，如需读取和编辑，需要使用tokens等命令

**分隔符和标点符号的联系和区别**

1. 分隔符就是原文中的标点符号，可以是一个标点符号也可以是多个
2. 但原文中的标点符号不一定是分隔符
3. 分隔符需要定义，即用”delims=”来指定，等于号后面跟被用来做分隔符的标点符号，该标点符号来自原文
4. 当有多个标点符号被定义为分隔符时，标点符号之间没有空格
5. 当用空格做分隔符时，空格应该放在其它用作分隔符的标点符号之后
6. 即使不使用delims，默认情况下，批处理也将空格作为分隔符。如文件名“Program Files”中含有空格时，批处理一般只读取空格前的“Program”，剩下的“Files”被忽略了

**注意**

1. “delims=”代表取消默认以空格作为分隔符
2. 当文件名或路径中含有空格时，需要用双引号括住。如果不使用双引号，括号内的内容将被当做文件名，如果文件名不存在，将提示“系统找不到……”
3. 如果不使用“delims=”，即使使用双引号，也只能显示第一个空格前面的字符串，后面的字符串将被默认忽略

```shell
@echo off
for /f "delims=," %%i in (test.txt) do echo %%i
pause

@echo off
for /f "delims=.，" %%i in (test.txt) do echo %%i
pause

@echo off
for /f "delims=" %%a in ("伟大的中国人民万岁 万岁 万万岁") do echo %%a
pause>nul

伟大的中国人民万岁 万岁 万万岁
```

### tokens= ：定点提取

tokens= 后面一般跟的是数字，也可以跟多个，每个数字之间用逗号分隔。含义是：提取第 X 节的字符串

`FOR /F "tokens=xym-n" %%I IN(Command1)DOCommand2`

**列与句子的联系与区别**

1. 文本内容由许多文字字符串组成，它们被标点符号分隔，两个标点符号之坚饽语句我们称为“句子”
2. 当标点符号被“tokens=”指定为分隔符之后，文字将被分成多个部分。对应的每行的每个部分我们称之为“列”
3. 一列可能是一个句子，也可能包含多个句子，视delims定义的标点符号而定

**多个数字的写法**

1. 以逗号分隔，每个数字就表示一个句子
2. 如果要提取的内容是连续的多“节”的话，那么，连续的数字可以只写最小值和最大值，中间用短横连接起来即可，比如 tokens=1,2,3,4,5 可以简写为 tokens=1-5
3. 字符串从左往右被切分成紧跟在 \* 之前的数值所表示的节数之后，字符串的其余部分保持不变，整体被*所表示的一个变量接收

```shell
@echo off
findstr /n .* 12.txt>>122.txt
set /p m=请输入要定位的行：
findstr /b "%m%" 122.txt
pause
```

### skip=n ：跳过无关内容

很多时候，有用的信息并不是贯穿文本内容的始终，而是位于第N行之后的行内，为了提高文本处理的效率，或
者不受多余信息的干扰，for /f 允许你跳过这些无用的行，直接从第N 1行开始处理，这个时候，就需要使用参数
 skip=n，其中，n是一个正整数，表示要跳过的行数。

```shell
@echo off
for /f "skip=2" %%i in (test.txt) do echo %%i
pause　　

这段代码将跳过头两行内容，从第3行起显示test.txt中的信息
```

### eol= ：忽略以指定字符打头的行

忽略以指定字符打头的行。

**注意**

1. for /f 语句默认忽略以分号打头的行内容
2. 如果要取消这个默认设置，可选择的办法是：
   1. 为eol=指定另外一个字符
   2. 使用 for /f "eol=" 语句，也就是说，强制指定字符为空，就像对付delims=一样

```shell
@echo off
for /f "eol=;" %%i in (test.txt) do echo %%i
pause
```



### 总结

**for /f 的六种写法**

1. `for /f %%i in (文件名) do (……)`
2. `for /f %%i in ('命令语句') do (……)`
3. `for /f %%i in ("字符串") do (……)`
4. `for /f "usebackq" %%i in ("文件名") do (……)`
5. `for /f "usebackq" %%i in (命令语句) do (……)` 注意：该句的命令语句中其实有一个符号，只是因为打不出来，记好有就可以了。应该是1左边按钮上的字符（英文状态）
6. `for /f "usebackq" %%i in ('字符串') do (……)`

**以上三种写法的使用场景**

1. 当你希望读取文本文件中的内容的话，第一个括号中不用任何符号包裹，应该使用的是第1条语句；例如：你
   想显示test.txt中的内容，那么，就使用 `for /f %%i in (test.txt) do echo %%i`
2. 当你读取的是命令语句执行结果中的内容的话，第一个括号中的命令语句必须使用单引号包裹，应该使用的是
   第2条语句；例如：你想显示当前目录下文件名中含有test字符串的文本文件的时候，应该使用
    `for /f %%i in ('dir /a-d /b *test*.txt') do echo %%i` 这样的语句
3. 当你要处理的是一个字符串的时候，第一个括号中的内容必须用双引号括起来，应该是用的是第3条语句；例
   如：当你想把bbs.bathome.cn这串字符中的点号换为短横线并显示出来的话，可以使用 
   `for /f "delims=. tokens=1-3" %%i in ("bbs.bathome.cn") do echo %%i-%%j-%%k` 这样的语句。

> 4、5、6由1、2、3发展而来，他们有这样的对应关系：1-->4、2-->5、3-->6

### for /d ：查询目录

`FOR /D %%variable IN (set) DO command [command-parameters]`

```shell
@echo off 
for /d %%i in (c:\*) do echo %%i 
pause

执行后会把C盘根目录下的全部目录名字打印出来,而文件则不显示

@echo off 
for /d %%i in (???) do echo %%i 
pause

把当前路径下文件夹的名字只有1~3个字母的打出来

@echo off 
for /d %%i in ("C:\Program Files\*") do echo %%i 
pause

执行后就会显示C:\Program File目录下的所有文件夹名称了，"C:\Program Files\*" 这里用了双引号是因为目录中有空格

@echo off 
for /d %%i in (*)do rd /s /q "%%i"
pause

这里是删除当前目录下的文件夹，但不会删除文件。
先用for /d 取得当前目录所有的文件夹,注意，是文件夹(如果是for /r ，则是文件)
然后再分别用rd命令把它（文件夹）删除。那么剩下的都是文件了。
```

### for /r ：遍历搜索

`FOR /R [[drive:]path] %%variable IN (set) DO command [command-parameters]`

> 该命令不会遍历隐藏文件

检查以  [drive:]path  为根的目录树，指向每个目录中的 FOR  语句。如果在  /R  后没有指定目录，则使用当前
目录。如果集仅为一个单点(.)字符，则枚举该目录树。

这个R可以理解为recursive的缩写，也就是递归的意思。所谓递归简单点来说就是对其目录及其子目录的所有文件进行操作。

**注意**

1. set 中的文件名如果含有通配符(？或*)，则列举/R 参数指定的目录及其下面的所有子目录中与 set 相符合的
   所有文件，无相符文件的目录则不列举
2. 如果 set  中为具体文件名，不含通配符，则枚举该目录树（即列举该目录及其下面的所有子目录），而不管
   set 中的指定文件是否存在。这与前面所说的单点（.）枚举目录树是一个道理，单点代表当前目录，也可视为一个文件

```shell
@echo off 
for /r c:\ %%i in (*.exe) do echo %%i 
pause

把 C 盘根目录,和每个目录的子目录下面全部的EXE 文件都列出来了

@echo off 
for /r %%i in (*.exe) do echo %%i 
pause

这个命令前面没加那个 c:\ （也就是搜索路径）,这样它就会以当前目录为搜索路径

@echo off 
for /r c:\ %%i in (boot.ini) do echo %%i 
pause

执行后，它不管boot.ini是否存在而将枚举c 盘所有目录

@echo off 
for /r c:\ %%i in (boot.ini) do if exist %%i echo %%i 
pause

只列举 boot.ini 存在的目录
```

### for /l ：建立序列

`FOR /L %%variable IN (start,step,end) DO command [command-parameters]`

该语句中(start,step,end)表示以增量或者递减形式从开始到结束的一个数字序列。因此，(1,1,5)将产生序列1 2 3
 4 5，(5,-1,1) 将产生序列5 4 3 2 1

/l参数将对start与end两个数值进行比较来执行迭代变量,如果star小于end,就会执行该命令。如果迭代变量超过end值，则命令解释程序退出此循环。

```shell
@echo off 
for /l %%i in (1,1,5) do echo %%i 
pause

执行看效果,他会打印从 1 2 3 4 5这样5 个数字，而且每个数字占一行。 
(1,1,5)这个参数也就是表示从 1 开始每次加 1 直到5 终止!

@echo off 
for /l %%i in (1,1,5) do start cmd 
pause

执行之后弹出了5个CMD窗口
把那个start cmd 改成md %%i  这样我们就会名为1 2 3 4 5的5个目录了！
```

# 四、文件操作

##  mkdir/md ：创建目录

`MKDIR [drive:]path`
`MD [drive:]path`

如果命令扩展被启用，MKDIR 会如下改变:

如果需要，MKDIR 会在路径中创建中级目录。例如: 假设 \a 不存在，那么:
`mkdir \a\b\c\d`

与:

```shell
mkdir \a
chdir \a
mkdir b
chdir b
mkdir c
chdir c
mkdir d
```

相同。如果扩展被停用，则需要键入 mkdir \a\b\c\d。

## rd 删除一个目录

`rmdir [/s] [/q] [driver:]path`

`rd  [/s] [/q] [driver:]path`

**参数**

`/s` ：除目录本身外，还将删除指定目录下的所有子目录和文件。用于删除目录树

`/q` ：安静模式。删除目录树时不要求确认

## del 删除一个或数个文件

`DEL [/P] [/F] [/S] [/Q] [/A[[:]attributes]] names`

`ERASE [/P] [/F] [/S] [/Q] [/A[[:]attributes]] names`

**参数**

`names` ：指定一个或多个文件或者目录列表。通配符可用来删除多个文件。如果指定了一个目录，该目录中的所有文件都会被删除

`/p` ：删除每一个文件之前提示确认

`/f` ：强制删除只读文件

`/s` ：删除所有子目录中的指定的文件

`/q` ：安静模式。删除全局通配符时，不需要确认

`/a` ：根据属性选择要删除的文件

属性：`R` ——只读文件，`S` ——系统文件， `H` ——隐藏文件， `A` ——存档文件， `I` ——无内容索引文件， `L` ——重分析点， `-` ——表示否的前缀

# 遗留问题

1. 如果将 ftype 的结果写入一个文本文件中

# 实用

## 文件

1. `cd.>123.txt` ：创建 0 字节的文本文件

